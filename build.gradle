import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.6.10"
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.6.10"
}

repositories {
    mavenCentral()
}

subprojects {
    group 'at.reisishot.mise'
    version '1.0-SNAPSHOT'

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

    compileKotlin {
        kotlinOptions {
            jvmTarget = "16"
            useIR = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "16"
            useIR = true
        }
    }

    repositories {
        mavenCentral()
    }

    compileJava {
        options.compilerArgs = Arrays.asList(
                "--add-exports", "java.base/java.util=ALL-UNNAMED"
        )
        options.forkOptions.executable = "javac" // required for above?
    }

    dependencies {
        // https://kotlinlang.org/releases.html#release-details
        def kotlinx_html_version = '0.7.3'
        def kotlin_version = '1.6.10'
        def kotlinx_coroutines_version = '1.6.0'
        def kotlinx_serialization_version = '1.3.1'
        def assertk_version = "0.25"
        def junit_version = "5.8.2"

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinx_coroutines_version"
        implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinx_html_version"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"


        testImplementation("com.willowtreeapps.assertk:assertk-jvm:$assertk_version")
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    }

    tasks.test {
        useJUnitPlatform {
            includeEngines("junit-jupiter")
        }

        testLogging {
            exceptionFormat = TestExceptionFormat.FULL
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }
}
